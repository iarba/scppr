#version 330 core

#define MAX_LIGHTS 32

struct material_t
{
  sampler2D diffuse;
  sampler2D specular;
  float shininess;
};

struct light_t
{
  vec3 position;
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
  float strength;
};

in vec3 f_pos;
in vec2 f_coord;
in vec3 f_norm;
in vec3 light_pos;

out vec4 color;

uniform material_t material;
uniform int light_no;
uniform light_t lights[MAX_LIGHTS];

vec4 point_light(light_t light, vec3 norm, vec3 view_dir)
{
  vec3 light_dir = normalize(light.position - f_pos);
  vec3 reflect_dir = reflect(-light_dir, norm);
  float distance = length(light.position - f_pos);
  float attenuation = max(1 - distance / light.strength, 0);
  float diffuse_strength = max(dot(norm, light_dir), 0.0);
  float specular_strength = 0.5 * pow(max(dot(view_dir, reflect_dir), 0.0), material.shininess);

  vec4 ambient_light  = attenuation * vec4(light.ambient, 1)  *                     texture(material.diffuse, f_coord);
  vec4 diffuse_light  = attenuation * vec4(light.diffuse, 1)  * diffuse_strength  * texture(material.diffuse, f_coord);
  vec4 specular_light = attenuation * vec4(light.specular, 1) * specular_strength * texture(material.specular, f_coord);

  return ambient_light + diffuse_light + specular_light;
}

void main()
{
  if(texture(material.diffuse, f_coord).a < 0.9)
  {
    discard;
  }
  vec3 norm = normalize(f_norm);
  vec3 view_dir = normalize(-f_pos);
  vec4 acc = vec4(0, 0, 0, 0);
  for(int i = 0; i < light_no; i++)
  {
    acc.a *= light_no;
    acc += point_light(lights[i], norm, view_dir);
    acc.a /= light_no;
  }
  color = acc;
}
